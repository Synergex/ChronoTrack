;;**********************************************************************
;;
;; Title:       RequestPreStore.dbc
;;
;; Type:        Class
;;
;; Description: Class defining the PreStore for the Request
;;              data entity event handler
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    ;;;<summary>
    ;;;Event handling class.  Used to allow DataEntity classes to define
    ;;;an event handling class which will be executed prior to a store operation.
    ;;;This allows for additional processing proir to the record being stored in the file
    ;;;</summary>
    public class RequestPreStore extends SynPSG.Core.DataEntities.PreStore

        ;;;<summary>
        ;;;Method that must be implemented by the DataEntity class when PreStoreProcessing
        ;;;events are required to be handled.
        ;;;</summary>
        ;;;<param name=sender>DataEntiry object</param>
        ;;;<param name=e>PreStoreEventArgs</param>
        public override method EventMethod   ,void
            in      req sender              ,@SynPSG.Core.DataEntities.SDMSio
            inout   req e                   ,@SynPSG.Core.DataEntities.PreStoreEventArgs
            endparams

            .include "request" repository, record = "request" ,end

            record localRecord
                status      ,boolean
                nextID      ,d ^size(request.request_ID)
                now         ,a20
            endrecord

        proc

            init localRecord
            now = %datetime()

            request = e.NewData

            debugInfo("RequestPreStore::EventMethod() : Attempt to locate last record")

            status = e.IOObject.ReadLast(Q_PRIMARY, false)
            if (status)
            then
            begin
                request = e.IOObject.DataArea
                nextID = request.request_ID
                incr nextID
            end
            else
            begin
                ;;must be the first in the file
                nextID = 1
            end

            request = e.NewData
            request.request_ID = nextID

            debugInfo("RequestPreStore::EventMethod() : setting new request ID to " + string(request.request_ID))

            ;;change tracking
            request.created_by = UserDataEntity.LoggedInUser
            request.created_date = now(1:8)

            ;;data integrity validations
            upcase request.Customer_id
            upcase request.Resource
            upcase request.Created_by

            ;set unique key for SQL replication
            request.replication_key = now

            e.NewData = request

            mreturn

        endmethod
    endclass

endnamespace
