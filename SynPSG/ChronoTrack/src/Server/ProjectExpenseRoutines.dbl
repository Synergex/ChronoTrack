;;**********************************************************************
;;
;; Title:       ProjectExpenseRoutines.dbl
;;
;; Type:        Subroutine (collection)
;;
;; Description: routines to allow remote access to the projectExpense data file
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"
.include "PROJECT_EXPENSE" repository, structure="sExpense", end

;;; <summary>
;;; get_expense
;;; </summary>
;;; <remarks>
;;; using the passed key locate te required project expense record on the primary key
;;; </remark>
;;; <param name=projectID>passed project ID</param>
;;; <param name=expenseID>passed expense ID</param>
;;; <param name=projectExpenseData>returned record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_expense                ,boolean
    required in  userToken          ,a22
    required in  projectID          ,d8
    required in  expenseID          ,d6
    required out projectExpenseData ,sExpense
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;attempt to locate given key value
    result = ProjectExpenseDataEntity.Instance.IO.Locate(%string(projectID,"XXXXXXXX")+%string(expenseID,"XXXXXX"),Q_PRIMARY,false)

    ;;set the data area
    projectExpenseData = ProjectExpenseDataEntity.Instance.IO.DataArea

    ;;record any possible error message
    lastErrorMsg = ProjectExpenseDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; create_expense
;;; </summary>
;;; <remarks>
;;; pass in the record data and attempt to create it
;;; </remark>
;;; <param name=projectExpenseData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function create_expense                 ,boolean
    required in    userToken            ,a22
    required inout projectExpenseData   ,sExpense
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectExpenseDataEntity.Instance.IO.DataArea = projectExpenseData

    ;;create the record
    result = ProjectExpenseDataEntity.Instance.IO.Create()
    projectExpenseData = ProjectExpenseDataEntity.Instance.IO.DataArea
    lastErrorMsg = ProjectExpenseDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; get_projectExpense_table
;;; return the projectExpense data as a Array List
;;; </summary>
;;; <remarks>
;;; Return the contents of the projectExpense file as an array list which is
;;; then converted to a DataTable by xfNetLink.NET
;;; </remark>
;;; <param name=projectID>passed project ID</param>
;;; <param name=projectExpenseTable>returned expenses table</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_projectExpense_table       ,boolean
    required in  userToken              ,a22
    required in  projectID              ,d8
    required out projectExpenseTable    ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    if (projectID) then
        projectExpenseTable = ProjectExpenseDataEntity.Instance.DataCollection.Collection(%string(projectID,"XXXXXXXX"),Q_PRIMARY)
    else
        projectExpenseTable = ProjectExpenseDataEntity.Instance.DataCollection.Collection()

    freturn true

endfunction

;;; <summary>
;;; update_expense
;;; </summary>
;;; <remarks>
;;; pass in the modified record and attempt an update
;;; </remark>
;;; <param name=projectExpenseData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function update_expense             ,boolean
    required in  userToken          ,a22
    required in  projectExpenseData ,sExpense
    required out newGrfa            ,a22
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectExpenseDataEntity.Instance.IO.DataArea = projectExpenseData

    ;;update the record
    result = ProjectExpenseDataEntity.Instance.IO.CompareAndSave()
    lastErrorMsg = ProjectExpenseDataEntity.Instance.IO.LastErrorMessage

    newGrfa = ProjectExpenseDataEntity.Instance.IO.GRFA

    freturn result

endfunction

;;; <summary>
;;; delete_expense
;;; </summary>
;;; <remarks>
;;; pass in the record to delete
;;; </remark>
;;; <param name=projectExpenseData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function delete_expense             ,boolean
    required in  userToken          ,a22
    required in  projectExpenseData ,sExpense
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectExpenseDataEntity.Instance.IO.DataArea = projectExpenseData

    ;;delete the record
    result = ProjectExpenseDataEntity.Instance.IO.FindAndDelete()
    lastErrorMsg = ProjectExpenseDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

