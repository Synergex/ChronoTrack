;;**********************************************************************
;;
;; Title:       ProjectDataEntity.dbc
;;
;; Type:        Class
;;
;; Description: Class to allow access to the Project data file.
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    public sealed class ProjectDataEntity extends SynPSG.Core.DataEntities.SDMSBase

        ;;allow the class to be referenced as a singleton
        private static mInternalData    ,@ProjectDataEntity

        public static property Instance ,@ProjectDataEntity
        method get
        proc
            if (mInternalData == ^null)
                mInternalData = new ProjectDataEntity(true, TableView.Full)
            mreturn mInternalData
        endmethod
        endproperty

        ;;-------------------------------------------------------------------------
        ;;private fields
        ;;-------------------------------------------------------------------------

        ;;;<summary> private user record area </summary>
        .include 'project' repository, private record = "mProject", prefix="m"  ,end
        .include 'project' repository, private structure = "sProject", end

        ;;*************************************************************************

        ;;; <summary>
        ;;; Constructor
        ;;; </summary>
        ;;; <remarks>
        ;;; Constructor defines the file name and opens the file.  Creates the
        ;;; required event handler classes
        ;;; </remarks>
        ;;; <param name=viewType>type of table/collection view to provide</param>
        ;;; <param name=noDotNet>Pass TRUE to prevent the .NET framework being loaded</param>
        public method ProjectDataEntity
            in  req noDotNet            ,boolean
            in  req viewType            ,SynPSG.Core.DataEntities.TableView
            endparams
            parent("DAT:project.ism",                ;;file name
            &   new ProjectPreStore(),                ;;pre store event handler
            &   new ProjectPostStore(),               ;;post store event handler
            &   new ProjectPreUpdate(),               ;;pre update event handler
            &   ^null,                                ;;update conflict event handler
            &   new ProjectFormatTrans(),             ;;format transaction view event handler
            &   new ProjectOnDelete(),                ;;on delete event handler
            &   viewType,                             ;;table/collection view type
            &   "Project",                            ;;tableName
            &   new ProjectDefineTable(),             ;;define DataTable columns event handler
            &   new ProjectUpdateFields(),            ;;field update event handler
            &   "ProjectCollection",                  ;;collection name
            &   (@sProject) str,                      ;;collection object
            &   new ProjectCollectionLoad(),          ;;load collection event handler
            &   noDotNet)                             ;;do we load the framework?

            record
                str ,sProject
            endrecord
        proc

            if (viewType == TableView.Breif)
                this.DataTable.IncludeGRFA = false

            this.IO.SetReplicationDetails("PROJECT", mProject, mProject.mreplication_key)

            mreturn

        endmethod

        ;;*************************************************************************

        ;;; <summary>
        ;;; Expose the data as a DataTable object
        ;;; </summary>
        ;;; <remarks>
        ;;; Expose the data as a DataTable object.  This method returns
        ;;; all data in the file as a DataTable unless the filter argument is passed.
        ;;; </remarks>
        ;;; <param name=primaryKey>optionally pass a filter on the primary key</param>
        ;;; <returns>Populated DataTable</returns>
.ifdef OS_WINDOWSNT
        public method ResultsTable  ,@System.Data.DataTable
            in  opt primaryKey      ,a
            endparams

            record locrec
                row     ,@DataRow
                status  ,boolean
                gotKey  ,boolean
                tmpID   ,a^size(mProject.mProject_ID)
            endrecord
        proc
            init locrec
            this.DataTable.Clear()

            ;;now add the actual data
            if (^passed(primaryKey))
            then
            begin
                gotKey = true
                tmpID = primaryKey
                status = this.IO.Locate(tmpID, Q_PRIMARY, false)
            end
            else
            begin
                gotKey = false
                status = this.IO.ReadFirst(Q_PRIMARY, false)
            end

            while ((status && !gotKey) || (status && gotKey && this.IO.PrimaryKeyValue .eq. tmpID))
            begin
                debugInfo("ProjectDataEntity::DataTable : Creating a new row")

                row = this.DataTable.Table.NewRow()

                mProject = this.IO.DataArea

                moveToRow(row)

                debugInfo("ProjectDataEntity::DataTable : Row added to table, key : " + string(mProject.mProject_ID))

                status = this.IO.ReadNext(false)
            end

            ;;commit the added rows to the table
            this.DataTable.Table.AcceptChanges()

            mreturn this.DataTable.Table

        endmethod
.endc
        ;;*************************************************************************

        ;;; <summary>
        ;;; Routine to set default values
        ;;; </summary>
        ;;; <param name="currentUser">pass in the current user</param>
        public method SetDefaultValues  ,void
            in  req currentUser         ,string
            endparams

            record
                sysDateTime ,a20
                sysDate     ,d8@sysDateTime
                sysYear     ,d4@sysDateTime + 4
                sysMonth    ,d2@sysDateTime + 6
                sysDay      ,d2@sysDateTime + 8
                sysTime     ,d4@sysDateTime + 10
                sysHour     ,d2@sysDateTime + 10
                sysMinute   ,d2@sysDateTime + 12
            endrecord

        proc
            ;;set default values
            sysDateTime = datetime()

            ;;default the project ID to todays date
            init mProject
            ;;set project number to "create"
            mProject.mProject_ID = 99999999
            mProject.mStart_Date = sysDate
            mProject.mEnd_Date = sysDate
            ;;status 0 = created
            mProject.mCurrent_Status = 0
            mProject.mStatus_Date = sysDate
            mProject.mCoordinator = currentUser
            mProject.mCreated_By = currentUser
            mProject.mCreated_Date = sysDate

            this.IO.DataArea = mProject

            mreturn

        endmethod

        ;;*************************************************************************

        ;;; <summary>
        ;;; create a new default project
        ;;; </summary>
        ;;; <param name="currentUser">pass in the current user</param>
        ;;; <returns>DataTable</returns>
.ifdef OS_WINDOWSNT
        public method NewProject    ,@System.Data.DataTable
.else
        public method NewProject    ,@String
.endc
            in  req currentUser     ,string
            endparams

.ifdef OS_WINDOWSNT
            record
                row     ,@DataRow
            endrecord
.endc
        proc
.ifdef OS_WINDOWSNT
            this.DataTable.Clear()
            row = this.DataTable.Table.NewRow()

            SetDefaultValues(currentUser)

            moveToRow(row)

            debugInfo("ProjectDataEntity::NewProject : Row added to table, key : " + %string(mProject.mProject_ID))

            mreturn this.DataTable.Table
.else
            SetDefaultValues(currentUser)

            mreturn mProject
.endc

        endmethod

        ;;*************************************************************************
        ;;private mehtod to move the current reocrd to the passed row
        ;;*************************************************************************
.ifdef OS_WINDOWSNT
        private method moveToRow    ,void
            inout   req row         ,@System.Data.DataRow
        proc

            row[0] = Conversion.ToNetGRFA(this.IO.GRFA)
            row[1] = Conversion.ToNetInteger(mProject.mProject_ID)
            row[2] = Conversion.ToNetString(mProject.mCustomer_ID)
            row[3] = Conversion.ToNetString(mProject.mContract_ID)
            row[4] = Conversion.ToNetInteger(mProject.mContract_Project_ID)
            row[5] = Conversion.ToNetString(mProject.mDescription)
            row[6] = Conversion.ToNetDateTime(mProject.mStart_Date)
            row[7] = Conversion.ToNetDateTime(mProject.mEnd_Date)
            row[8] = Conversion.ToNetInteger(mProject.mCurrent_Status)
            row[9] = Conversion.ToNetDateTime(mProject.mStatus_Date)
            row[10] = Conversion.ToNetString(mProject.mLead_Consultant)
            row[11] = Conversion.ToNetString(mProject.mCoordinator)
            row[12] = Conversion.ToNetInteger(mProject.mApproved_By_First)
            row[13] = Conversion.ToNetInteger(mProject.mApproved_By_Last)
            row[14] = Conversion.ToNetDateTime(mProject.mApproved_Date)
            row[15] = Conversion.ToNetInteger(mProject.mApproval_Location)
            row[16] = Conversion.ToNetInteger(mProject.mWork_Location_ID)
            row[17] = Conversion.ToNetString(mProject.mCreated_By)
            row[18] = Conversion.ToNetDateTime(mProject.mCreated_Date)
            row[19] = Conversion.ToNetString(mProject.mModified_By)
            row[20] = Conversion.ToNetDateTime(mProject.mModified_Date)
            row[21] = Conversion.ToNetString(mProject.mText)
            row[22] = Conversion.ToNetString(mProject.mReplication_Key)

            this.DataTable.Table.Rows.Add(row)

            mreturn

        endmethod
.endc
        public property CustomerCode    ,string
            method get
            proc
                mreturn mProject.mCustomer_ID
            endmethod
        endproperty
    endclass

endnamespace


