;;**********************************************************************
;;
;; Title:       ProjectNoteRoutines.dbl
;;
;; Type:        Subroutine (collection)
;;
;; Description: routines to allow remote access to the ProjectNote data file
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"
.include "PROJECT_NOTE" repository, structure="sNote", end

;;; <summary>
;;; get_note
;;; </summary>
;;; <remarks>
;;; using the passed key locate te required project expense record on the primary key
;;; </remark>
;;; <param name=projectID>passed project ID</param>
;;; <param name=noteID>passed note ID</param>
;;; <param name=projectNoteData>returned record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_note                   ,boolean
    required in  userToken          ,a22
    required in  projectID          ,d8
    required in  noteID             ,d4
    required out projectNoteData    ,sNote
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;attempt to locate given key value
    result = ProjectNoteDataEntity.Instance.IO.Locate(%string(projectID,"XXXXXXXX")+%string(noteID,"XXXX"),Q_PRIMARY,false)

    ;;set the data area
    projectNoteData = ProjectNoteDataEntity.Instance.IO.DataArea

    ;;record any possible error message
    lastErrorMsg = ProjectNoteDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; create_note
;;; </summary>
;;; <remarks>
;;; pass in the record data and attempt to create it
;;; </remark>
;;; <param name=projectNoteData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function create_note                ,boolean
    required in    userToken        ,a22
    required inout projectNoteData  ,sNote
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectNoteDataEntity.Instance.IO.DataArea = projectNoteData

    ;;create the record
    result = ProjectNoteDataEntity.Instance.IO.Create()
    projectNoteData = ProjectNoteDataEntity.Instance.IO.DataArea
    lastErrorMsg = ProjectNoteDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; get_projectNote_table
;;; return the ProjectNote data as a Array List
;;; </summary>
;;; <remarks>
;;; Return the contents of the ProjectNote file as an array list which is
;;; then converted to a DataTable by xfNetLink.NET
;;; </remark>
;;; <param name=projectID>passed project ID</param>
;;; <param name=projectNoteTable>returned table of project notes</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_ProjectNote_table  ,boolean
    required in  userToken          ,a22
    required in  projectID          ,d8
    required out projectNoteTable   ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    if (projectID) then
        projectNoteTable = ProjectNoteDataEntity.Instance.DataCollection.Collection(%string(projectID,"XXXXXXXX"),Q_PRIMARY)
    else
        projectNoteTable = ProjectNoteDataEntity.Instance.DataCollection.Collection()

    freturn true

endfunction

;;; <summary>
;;; count_projectNote_table
;;; count the number of matching records in the file
;;; </summary>
;;; <remarks>
;;; Count the number of matching records in the file and return the count
;;; </remark>
;;; <param name=projectID>passed project ID</param>
;;; <return type="Int">count of records</returns>
function Count_ProjectNote_table    ,int
    required in  userToken  ,a22
    required in  projectID  ,d8
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    freturn ProjectNoteDataEntity.Instance.DataCollection.Count(%string(projectID,"XXXXXXXX"),Q_PRIMARY)

endfunction

;;; <summary>
;;; update_note
;;; </summary>
;;; <remarks>
;;; pass in the modified record and attempt an update
;;; </remark>
;;; <param name=projectNoteData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function update_note                ,boolean
    required in  userToken          ,a22
    required in  projectNoteData    ,sNote
    required out newGrfa            ,a22
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectNoteDataEntity.Instance.IO.DataArea = projectNoteData

    ;;update the record
    result = ProjectNoteDataEntity.Instance.IO.CompareAndSave()
    lastErrorMsg = ProjectNoteDataEntity.Instance.IO.LastErrorMessage

    newGrfa = ProjectNoteDataEntity.Instance.IO.GRFA

    freturn result

endfunction

;;; <summary>
;;; delete_note
;;; </summary>
;;; <remarks>
;;; pass in the record to delete
;;; </remark>
;;; <param name=projectNoteData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function delete_note                ,boolean
    required in  userToken          ,a22
    required in  projectNoteData    ,sNote
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    ProjectNoteDataEntity.Instance.IO.DataArea = projectNoteData

    ;;delete the record
    result = ProjectNoteDataEntity.Instance.IO.FindAndDelete()
    lastErrorMsg = ProjectNoteDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

