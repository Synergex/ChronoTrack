;;**********************************************************************
;;
;; Title:       CustomerLocationPreStore.dbc
;;
;; Type:        Class
;;
;; Description: Class defining the PreStore for the customer location
;;              data entity event handler
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    ;;;<summary>
    ;;;Event handling class.  Used to allow DataEntity classes to define
    ;;;an event handling class which will be executed prior to a store operation.
    ;;;This allows for additional processing proir to the record being stored in the file
    ;;;</summary>
    public class CustomerLocationPreStore extends SynPSG.Core.DataEntities.PreStore

        ;;;<summary>
        ;;;Method that must be implemented by the DataEntity class when PreStoreProcessing
        ;;;events are required to be handled.
        ;;;</summary>
        ;;;<param name=sender>DataEntiry object</param>
        ;;;<param name=e>PreStoreEventArgs</param>
        public override method EventMethod   ,void
            in      req sender              ,@SynPSG.Core.DataEntities.SDMSio
            inout   req e                   ,@SynPSG.Core.DataEntities.PreStoreEventArgs
            endparams

            .include "customer_location" repository, record = "location" ,end

            record localRecord
                status  ,boolean
                index   ,int
            endrecord

        proc

            ;;get the data as it currently is
            location = e.NewData
            init localRecord

            debugInfo("CustomerLocationPreStore::EventMethod() : Attempt to locate last record using key " + location.customer_ID)

            ;;read through all customer locations for the matching customer
            status = e.IOObject.Locate(location.customer_ID, Q_PRIMARY, false)
            while (status && e.IOObject.PrimaryKeyValue .eq. location.customer_ID)
            begin
                ;;get the data read
                location = e.IOObject.DataArea
                ;;store the index
                index = location.location_ID

                debugInfo("CustomerLocationPreStore::EventMethod() : Found : " + location.customer_ID + " index " + string(location.location_ID))

                ;;see if there are any further matching records
                status = e.IOObject.ReadNext(false)
            end

            ;restore the pssed data area and set the new index
            location = e.NewData
            location.location_ID = (index+=1)

            ;;data integrity validations
            upcase location.customer_id
            upcase location.state
            upcase location.zip
            upcase location.country_id

            ;set unique key for SQL replication
            location.replication_key = %datetime()

            ;;send the updated data back
            e.NewData = location

            mreturn

        endmethod
    endclass

endnamespace
