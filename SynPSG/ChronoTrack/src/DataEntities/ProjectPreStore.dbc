;;**********************************************************************
;;
;; Title:       ProjectPreStore.dbc
;;
;; Type:        Class
;;
;; Description: Class defining the PreStore for the Project
;;              data entity event handler
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    ;;;<summary>
    ;;;Event handling class.  Used to allow DataEntity classes to define
    ;;;an event handling class which will be executed prior to a store operation.
    ;;;This allows for additional processing proir to the record being stored in the file
    ;;;</summary>
    public class ProjectPreStore extends SynPSG.Core.DataEntities.PreStore

        ;;;<summary>
        ;;;Method that must be implemented by the DataEntity class when PreStoreProcessing
        ;;;events are required to be handled.
        ;;;</summary>
        ;;;<param name=sender>DataEntiry object</param>
        ;;;<param name=e>PreStoreEventArgs</param>
        public override method EventMethod   ,void
            in      req sender              ,@SynPSG.Core.DataEntities.SDMSio
            inout   req e                   ,@SynPSG.Core.DataEntities.PreStoreEventArgs
            endparams

            .include "project" repository, record = "project" ,end
            .include "project" repository, record = "tmpProject" ,end

            record localRecord
                status      ,boolean
                nextID      ,d ^size(project.project_ID)
                now         ,a20
            endrecord

        proc

            init localRecord
            now = %datetime()

            ;;Find the last used project number and increment it for the new project

            debugInfo("ProjectPreStore::EventMethod() : Attempt to locate last record")

            status = e.IOObject.ReadLast(Q_PRIMARY, false)
            if (status) then
            begin
                project = e.IOObject.DataArea
                nextID = project.project_ID
                project = e.NewData
                incr nextID
            end
            else
            begin
                ;;must be the first in the file
                nextID = 1
                project = e.NewData
            end

            project.project_ID = nextID

            debugInfo("ProjectPreStore::EventMethod() : setting new project ID to " + string(project.project_ID))

            ;TODO:  Need to read through all projects for the same contract and then set
            ;       project.Contract_project_id to the next highest value.

            ;;Set the next sub project ID within this customer contract
            ;project.Contract_project_id = ?????

            upcase project.Contract_id

            status = e.IOObject.ReadFirst(Q_PRIMARY, false)
            if (status) then
            begin
                while (status)
                begin
                    tmpProject = e.IOObject.DataArea
                    if (tmpProject.contract_id == project.contract_id)
                        nextID = tmpProject.contract_project_id
                    status = e.IOObject.ReadNext(false)
                end
                ;;make it the next contract ID number
                incr nextID
            end
            else
            begin
                ;;must be the first in the file
                nextID = 1
            end

            project.contract_project_id = nextID

            ;;change tracking
            project.Created_by = UserDataEntity.LoggedInUser
            project.Created_date = now(1:8)
            project.Modified_by = UserDataEntity.LoggedInUser
            project.Modified_date = now(1:8)

            ;;new project defaults
            ;status to created!!
            project.Current_status = 0
            project.Status_date = now(1:8)

            ;;data integrity validations
            upcase project.Customer_id
            upcase project.Contract_id
            upcase project.Lead_consultant
            upcase project.Coordinator
            upcase project.Created_by
            upcase project.Modified_by

            ;set unique key for SQL replication
            project.replication_key = now

            e.NewData = project

            mreturn

        endmethod
    endclass

endnamespace
