;;**********************************************************************
;;
;; Title:       CustomerRoutines.dbl
;;
;; Type:        Subroutine (collection)
;;
;; Description: routines to allow remote access to the Customer data file
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"
.include "CUSTOMER"       repository, structure="sCustomer", end
.include "CUSTOMER_BREIF" repository, structure="sCustomerBrief", end

;;; <summary>
;;; get_customer
;;; </summary>
;;; <remarks>
;;; using the passed key locate te required customer record on the primary key
;;; </remark>
;;; <param name=customerID>pass required customer ID</param>
;;; <param name=customerData>return matching record</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_customer           ,boolean
    required in  userToken      ,a22
    required in  customerID     ,a10
    required out customerData   ,sCustomer
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;attempt to locate given key value
    result = CustomerDataEntity.Instance.IO.Locate(customerID,Q_PRIMARY,false)

    ;;set the data area
    customerData = CustomerDataEntity.Instance.IO.DataArea

    ;;record any possible error message
    lastErrorMsg = CustomerDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; create_customer
;;; </summary>
;;; <remarks>
;;; pass in the record data and attempt to create it
;;; </remark>
;;; <param name=customerData>pass customer record</param>
;;; <return type="Boolean">true = success, false = error</returns>
function create_customer        ,boolean
    required in    userToken    ,a22
    required inout customerData ,sCustomer
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerDataEntity.Instance.IO.DataArea = customerData

    ;;create the record
    result = CustomerDataEntity.Instance.IO.Create()
    customerData = CustomerDataEntity.Instance.IO.DataArea
    lastErrorMsg = CustomerDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; get_customer_table
;;; return the Customer data as a Array List
;;; </summary>
;;; <remarks>
;;; Return the contents of the Customer file as an array list which is
;;; then converted to a DataTable by xfNetLink.NET
;;; </remark>
;;; <param name="customerTable">return the customer datatable</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_customer_table ,boolean
    required in  userToken      ,a22
    required out customerTable  ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    customerTable = CustomerDataEntity.Instance.DataCollection.Collection()

    freturn true

endfunction

;;; <summary>
;;; get_customer_table
;;; return the Customer data as a Array List
;;; </summary>
;;; <remarks>
;;; return only selected fields from the customer structure
;;; </remark>
;;; <param name="customerTable">return the brief cusomer datatable</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_customer_table_brief ,boolean
    required in  userToken      ,a22
    required out customerTable  ,@ArrayList
    endparams

    .include "DEF:Server.def"

    record
        bRec    ,sCustomerBrief
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    customerTable = CustomerDataEntity.Instance.DataCollection.Collection(TableView.Breif,(@sCustomerBrief)bRec)

    freturn true

endfunction

;;; <summary>
;;; update_customer
;;; </summary>
;;; <remarks>
;;; pass in the modified record and attempt an update
;;; </remark>
;;; <param name=customerData>pass customer record</param>
;;; <return type="Boolean">true = success, false = error</returns>
function update_customer        ,boolean
    required in  userToken      ,a22
    required in  customerData   ,sCustomer
    required out newGrfa        ,a22
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerDataEntity.Instance.IO.DataArea = customerData

    ;;update the record
    result = CustomerDataEntity.Instance.IO.CompareAndSave()
    lastErrorMsg = CustomerDataEntity.Instance.IO.LastErrorMessage

    newGrfa = CustomerDataEntity.Instance.IO.GRFA

    freturn result

endfunction

;;; <summary>
;;; delete_customer
;;; </summary>
;;; <remarks>
;;; pass in the record to delete
;;; </remark>
;;; <param name=customerData>pass customer record</param>
;;; <return type="Boolean">true = success, false = error</returns>
function delete_customer        ,boolean
    required in  userToken      ,a22
    required in  customerData   ,sCustomer
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerDataEntity.Instance.IO.DataArea = customerData

    ;;first delete linked records
    CustomerDataEntity.Instance.IO.DataConsistency(customerData.customer_ID)

    ;;delete the record
    result = CustomerDataEntity.Instance.IO.FindAndDelete()
    lastErrorMsg = CustomerDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

