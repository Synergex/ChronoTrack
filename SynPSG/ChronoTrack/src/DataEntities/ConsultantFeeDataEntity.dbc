;;**********************************************************************
;;
;; Title:       ConsultantFeeDataEntity.dbc
;;
;; Type:        Class
;;
;; Description: Class to allow access to the fee data file.
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    public sealed class ConsultantFeeDataEntity extends SynPSG.Core.DataEntities.SDMSBase

        ;;allow the class to be referenced as a singleton
        private static mInternalData    ,@ConsultantFeeDataEntity

        public static property Instance ,@ConsultantFeeDataEntity
        method get
        proc
            if (mInternalData == ^null)
                mInternalData = new ConsultantFeeDataEntity(true, TableView.Full)
            mreturn mInternalData
        endmethod
        endproperty

        ;;-------------------------------------------------------------------------
        ;;private fields
        ;;-------------------------------------------------------------------------

        ;;;<summary> private user record area </summary>
        .include 'consultant_fee' repository, private record = "mConsultantFee", prefix="m"  ,end
        .include 'consultant_fee' repository, private structure = "sConsultantFee", prefix="s"  ,end

        ;;*************************************************************************

        ;;; <summary>
        ;;; Constructor
        ;;; </summary>
        ;;; <remarks>
        ;;; Constructor defines the file name and opens the file.  Creates the
        ;;; required event handler classes
        ;;; </remarks>
        ;;; <param name=viewType>type of table/collection view to provide</param>
        ;;; <param name=noDotNet>Pass TRUE to prevent the .NET framework being loaded</param>
        public method ConsultantFeeDataEntity
            in  req noDotNet            ,boolean
            in  req viewType            ,SynPSG.Core.DataEntities.TableView
            endparams
            parent("DAT:fee.ism",                  ;;file name
            &   new ConsultantFeePreStore(),        ;;pre store event handler
            &   ^null,                              ;;post store event handler
            &   new ConsultantFeePreUpdate(),       ;;pre update event handler
            &   ^null,                              ;;update conflict event handler
            &   ^null,                              ;;format transaction view event handler
            &   ^null,                              ;;on delete event handler
            &   viewType,                           ;;table/collection view type
            &   "ConsultantFee",                    ;;tableName
            &   new ConsultantFeeDefineTable(),     ;;define DataTable columns event handler
            &   new ConsultantFeeUpdateFields(),    ;;field update event handler
            &   "ConsultantFeeCollection",          ;;collection name
            &   (@sConsultantFee) str,              ;;collection object
            &   ^null,                              ;;load collection event handler
            &   noDotNet)                           ;;do we load the framework?

            record
                str     ,sConsultantFee
            endrecord
        proc

            this.IO.SetReplicationDetails("CONSULTANT_FEE", mConsultantFee, mConsultantFee.mreplication_key)

            mreturn

        endmethod

        ;;*************************************************************************

        ;;; <summary>
        ;;; Expose the data as a DataTable object
        ;;; </summary>
        ;;; <remarks>
        ;;; Expose the data as a DataTable object.  This method returns
        ;;; all data in the file as a DataTable unless the filter argument is passed.
        ;;; </remarks>
        ;;; <param name=primaryKey>optionally pass a filter on the primary key</param>
        ;;; <returns>Populated DataTable</returns>
.ifdef OS_WINDOWSNT
        public method ResultsTable  ,@System.Data.DataTable
            in  opt primaryKey      ,a
            endparams

            record locrec
                row     ,@DataRow
                status  ,boolean
                gotKey  ,boolean
                tmpID   ,a^size(mConsultantFee.mUser_ID)
            endrecord
        proc
            init locrec
            this.DataTable.Clear()

            ;;now add the actual data
            if (^passed(primaryKey))
            then
            begin
                gotKey = true
                tmpID = primaryKey
                status = this.IO.Locate(tmpID, 0, false)
            end
            else
            begin
                gotKey = false
                status = this.IO.ReadFirst(Q_PRIMARY, false)
            end

            while ((status && !gotKey) || (status && gotKey && this.IO.PrimaryKeyValue .eq. tmpID))
            begin
                debugInfo("ConsultantFeeDataEntity::DataTable : Creating a new row")

                row = this.DataTable.Table.NewRow()

                mConsultantFee = this.IO.DataArea

                row[0] = Conversion.ToNetGRFA(this.IO.GRFA)
                row[1] = Conversion.ToNetString(mConsultantFee.mUser_ID)
                row[2] = Conversion.ToNetInteger(mConsultantFee.mFee_Type_ID)
                row[3] = Conversion.ToNetDecimal(mConsultantFee.mCost)
                row[4] = Conversion.ToNetString(mConsultantFee.mReplication_Key)

                this.DataTable.Table.Rows.Add(row)

                debugInfo("ConsultantFeeDataEntity::DataTable : Row added to table, key : " + mConsultantFee.mUser_ID)

                status = this.IO.ReadNext(false)
            end

            ;;commit the added rows to the table
            this.DataTable.Table.AcceptChanges()

            mreturn this.DataTable.Table

        endmethod
.endc
    endclass

endnamespace

