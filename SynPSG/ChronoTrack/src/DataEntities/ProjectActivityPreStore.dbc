;;**********************************************************************
;;
;; Title:       ProjectActivityPreStore.dbc
;;
;; Type:        Class
;;
;; Description: Class defining the PreStore for the project activity
;;              contanct data entity event handler
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    ;;;<summary>
    ;;;Event handling class.  Used to allow DataEntity classes to define
    ;;;an event handling class which will be executed prior to a store operation.
    ;;;This allows for additional processing proir to the record being stored in the file
    ;;;</summary>
    public class ProjectActivityPreStore extends SynPSG.Core.DataEntities.PreStore

        ;;;<summary>
        ;;;Method that must be implemented by the DataEntity class when PreStoreProcessing
        ;;;events are required to be handled.
        ;;;</summary>
        ;;;<param name=sender>DataEntiry object</param>
        ;;;<param name=e>PreStoreEventArgs</param>
        public override method EventMethod   ,void
            in      req sender              ,@SynPSG.Core.DataEntities.SDMSio
            inout   req e                   ,@SynPSG.Core.DataEntities.PreStoreEventArgs
            endparams

            .include "project_activity" repository, record = "activity" ,end

            record localRecord
                status      ,boolean
                index       ,int
                now         ,a20
            endrecord

        proc

            init localRecord

            now = %datetime()

            activity = e.NewData

            ;;read through all activities for this task to find the last one
            status = e.IOObject.Locate(%string(activity.project_id,'XXXXXXXX')+%string(activity.task_id,'XXX'), Q_PRIMARY, false)
            while (status && e.IOObject.PrimaryKeyValue .eq. %string(activity.project_id, 'XXXXXXXX')+%string(activity.task_id,'XXX'))
            begin
                ;;get the data read
                activity = e.IOObject.DataArea
                ;;store the index
                index = activity.activity_id

                ;;see if there are any further matching records
                status = e.IOObject.ReadNext(false)
            end

            activity = e.NewData
            activity.activity_id= (index + 1)

            activity.start_time = 0900
            activity.end_time = 1700

            ;;change tracking
            activity.Created_by = UserDataEntity.LoggedInUser
            activity.Created_date = now(1:8)
            activity.Modified_by = UserDataEntity.LoggedInUser
            activity.Modified_date = now(1:8)

            ;;data integrity validations
            upcase activity.created_by
            upcase activity.modified_by

            ;set unique key for SQL replication
            activity.replication_key = now

            e.NewData = activity

            mreturn

        endmethod
    endclass

endnamespace
