;;**********************************************************************
;;
;; Title:       FeeTypeRoutines.dbl
;;
;; Type:        Subroutine (collection)
;;
;; Description: routines to allow remote access to the FeeType data file
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"
.include "FEE_TYPE" repository, structure="sFeeType", end

;;; <summary>
;;; get_fee_type
;;; </summary>
;;; <remarks>
;;; using the passed key locate te required fee record on the primary key
;;; </remark>
;;; <param name=feeID>pass fee id to locate</param>
;;; <param name=feeTypeData>return located record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_fee_type           ,boolean
    required in  userToken      ,a22
    required in  feeID          ,d2
    required out feeTypeData    ,sFeeType
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;attempt to locate given key value
    result = FeeTypeDataEntity.Instance.IO.Locate(%string(feeID,"XX"),Q_PRIMARY,false)

    ;;set the data area
    feeTypeData = FeeTypeDataEntity.Instance.IO.DataArea

    ;;record any possible error message
    lastErrorMsg = FeeTypeDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; create_fee_type
;;; </summary>
;;; <remarks>
;;; pass in the record data and attempt to create it
;;; </remark>
;;; <param name=feeTypeData>pass record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function create_fee_type, boolean
    required in  userToken      ,a22
    required inout feeTypeData  ,sFeeType
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    FeeTypeDataEntity.Instance.IO.DataArea = feeTypeData

    ;;create the record
    result = FeeTypeDataEntity.Instance.IO.Create()
    feeTypeData = FeeTypeDataEntity.Instance.IO.DataArea
    lastErrorMsg = FeeTypeDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; get_FeeType_table
;;; return the FeeType data as a Array List
;;; </summary>
;;; <remarks>
;;; Return the contents of the FeeType file as an array list which is
;;; then converted to a DataTable by xfNetLink.NET
;;; </remark>
;;; <param name=feeTypeTable>return populated fee type table</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_FeeType_table  ,boolean
    required in  userToken      ,a22
    required out feeTypeTable   ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    feeTypeTable = FeeTypeDataEntity.Instance.DataCollection.Collection()

    freturn true

endfunction

;;; <summary>
;;; update_fee_type
;;; </summary>
;;; <remarks>
;;; pass in the modified record and attempt an update
;;; </remark>
;;; <param name=feeTypeData>pass record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function update_fee_type        ,boolean
    required in  userToken      ,a22
    required in  feeTypeData    ,sFeeType
    required out newGrfa        ,a22
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    FeeTypeDataEntity.Instance.IO.DataArea = feeTypeData

    ;;update the record
    result = FeeTypeDataEntity.Instance.IO.CompareAndSave()
    lastErrorMsg = FeeTypeDataEntity.Instance.IO.LastErrorMessage

    newGrfa = FeeTypeDataEntity.Instance.IO.GRFA

    freturn result

endfunction

;;; <summary>
;;; delete_fee_type
;;; </summary>
;;; <remarks>
;;; pass in the record to delete
;;; </remark>
;;; <param name=feeTypeData>pass record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function delete_fee_type        ,boolean
    required in  userToken      ,a22
    required in  feeTypeData    ,sFeeType
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    FeeTypeDataEntity.Instance.IO.DataArea = feeTypeData

    ;;delete the record
    result = FeeTypeDataEntity.Instance.IO.FindAndDelete()
    lastErrorMsg = FeeTypeDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

