;;**********************************************************************
;;
;; Title:       ProjectExpenseDataEntity.dbc
;;
;; Type:        Class
;;
;; Description: Class to allow access to the project expense data file.
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    public sealed class ProjectExpenseDataEntity extends SynPSG.Core.DataEntities.SDMSBase

        ;;allow the class to be referenced as a singleton
        private static mInternalData    ,@ProjectExpenseDataEntity

        public static property Instance ,@ProjectExpenseDataEntity
        method get
        proc
            if (mInternalData == ^null)
                mInternalData = new ProjectExpenseDataEntity(true, TableView.Full)
            mreturn mInternalData
        endmethod
        endproperty

        ;;-------------------------------------------------------------------------
        ;;private fields
        ;;-------------------------------------------------------------------------

        ;;;<summary> private user record area </summary>
        .include 'project_expense' repository, private record = "mProjectExpense", prefix="m"  ,end
        .include 'project_expense' repository, private structure = "sProjectExpense", prefix="s"  ,end

        ;;*************************************************************************

        ;;; <summary>
        ;;; Constructor
        ;;; </summary>
        ;;; <remarks>
        ;;; Constructor defines the file name and opens the file.  Creates the
        ;;; required event handler classes
        ;;; </remarks>
        ;;; <param name=viewType>type of table/collection view to provide</param>
        ;;; <param name=noDotNet>Pass TRUE to prevent the .NET framework being loaded</param>
        public method ProjectExpenseDataEntity
            in  req noDotNet            ,boolean
            in  req viewType            ,SynPSG.Core.DataEntities.TableView
            endparams
            parent("DAT:expense.ism",                ;;file name
            &   new ProjectExpensePreStore(),         ;;pre store event handler
            &   ^null,                                ;;post store event handler
            &   new ProjectExpensePreUpdate(),        ;;pre update event handler
            &   ^null,                                ;;update conflict event handler
            &   ^null,                                ;;format transaction view event handler
            &   ^null,                                ;;on delete event handler
            &   viewType,                             ;;table/collection view type
            &   "ProjectExpense",                     ;;tableName
            &   new ProjectExpenseDefineTable(),      ;;define DataTable columns event handler
            &   new ProjectExpenseUpdateFields(),     ;;field update event handler
            &   "ProjectExpenseCollection",           ;;collection name
            &   (@sProjectExpense) str,               ;;collection object
            &   ^null,                                ;;load collection event handler
            &   noDotNet)                             ;;do we load the framework?

            record
                str ,sProjectExpense
            endrecord
        proc

            this.IO.SetReplicationDetails("PROJECT_EXPENSE", mProjectExpense, mProjectExpense.mreplication_key)

            mreturn

        endmethod

        ;;*************************************************************************

        ;;; <summary>
        ;;; Expose the data as a DataTable object
        ;;; </summary>
        ;;; <remarks>
        ;;; Expose the data as a DataTable object.  This method returns
        ;;; all data in the file as a DataTable unless the filter argument is passed.
        ;;; </remarks>
        ;;; <param name=primaryKey>optionally pass a filter on the primary key</param>
        ;;; <returns>Populated DataTable</returns>
.ifdef OS_WINDOWSNT
        public method ResultsTable  ,@System.Data.DataTable
            in  opt primaryKey      ,a
            endparams

            record locrec
                row     ,@DataRow
                status  ,boolean
                gotKey  ,boolean
                tmpID   ,a^size(mProjectExpense.mProject_ID)
            endrecord
        proc
            init locrec
            this.DataTable.Clear()

            ;;now add the actual data
            if (^passed(primaryKey))
            then
            begin
                gotKey = true
                tmpID = primaryKey
                status = this.IO.Locate(tmpID, Q_PRIMARY, false)
            end
            else
            begin
                gotKey = false
                status = this.IO.ReadFirst(Q_PRIMARY, false)
            end

            while ((status && !gotKey) || (status && gotKey && this.IO.PrimaryKeyValue .eq. tmpID))
            begin
                debugInfo("ProjectExpenseDataEntity::DataTable : Creating a new row")

                row = this.DataTable.Table.NewRow()

                mProjectExpense = this.IO.DataArea

                row[0] = Conversion.ToNetGRFA(this.IO.GRFA)
                row[1] = Conversion.ToNetInteger(mProjectExpense.mProject_ID)
                row[2] = Conversion.ToNetInteger(mProjectExpense.mExpense_ID)
                row[3] = Conversion.ToNetDateTime(mProjectExpense.mExpense_Date)
                row[4] = Conversion.ToNetString(mProjectExpense.mExpense_Type)
                row[5] = Conversion.ToNetString(mProjectExpense.mDescription)
                row[6] = Conversion.ToNetString(mProjectExpense.mCurrency_Code)
                row[7] = Conversion.ToNetDecimal(mProjectExpense.mUnit_Price)
                row[8] = Conversion.ToNetDecimal(mProjectExpense.mQuantity)
                row[9] = Conversion.ToNetString(mProjectExpense.mExpensed_By)
                row[10] = Conversion.ToNetString(mProjectExpense.mEntered_By)
                row[11] = Conversion.ToNetDateTime(mProjectExpense.mEntered_Date)
                row[12] = Conversion.ToNetString(mProjectExpense.mModified_By)
                row[13] = Conversion.ToNetDateTime(mProjectExpense.mModified_Date)
                row[14] = Conversion.ToNetBoolean(mProjectExpense.mIs_Estimate)
                row[15] = Conversion.ToNetString(mProjectExpense.mReplication_Key)

                this.DataTable.Table.Rows.Add(row)

                debugInfo("ProjectExpenseDataEntity::DataTable : Row added to table, key : " + string(mProjectExpense.mProject_ID))

                status = this.IO.ReadNext(false)
            end

            ;;commit the added rows to the table
            this.DataTable.Table.AcceptChanges()

            mreturn this.DataTable.Table

        endmethod
.endc
    endclass

endnamespace

