;;**********************************************************************
;;
;; Title:       CustomerContactRoutines.dbl
;;
;; Type:        Subroutine (collection)
;;
;; Description: routines to allow remote access to the customer_contact
;;              data file
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"
.include "CUSTOMER_CONTACT" repository, structure="sContact", end

;;; <summary>
;;; get_customer_contact
;;; </summary>
;;; <remarks>
;;; using the passed key locate the required customer contact record on the primary key
;;; </remark>
;;; <param name=customerID>required customer ID</param>
;;; <param name=contactID>required contact ID</param>
;;; <param name=customerContactData>returned record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_customer_contact           ,boolean
    required in  userToken              ,a22
    required in  customerID             ,a10
    required in  contactID              ,d3
    required out customerContactData    ,sContact
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;attempt to locate given key value
    result = CustomerContactDataEntity.Instance.IO.Locate(customerID+%string(contactID,"XXX"),Q_PRIMARY,false)

    ;;set the data area
    customerContactData = CustomerContactDataEntity.Instance.IO.DataArea

    ;;record any possible error message
    lastErrorMsg = CustomerContactDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; create_customer_contact
;;; </summary>
;;; <remarks>
;;; pass in the record data and attempt to create it
;;; </remark>
;;; <param name=customerContactData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function create_customer_contact        ,boolean
    required in    userToken            ,a22
    required inout customerContactData  ,sContact
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerContactDataEntity.Instance.IO.DataArea = customerContactData

    ;;create the record
    result = CustomerContactDataEntity.Instance.IO.Create()
    customerContactData = CustomerContactDataEntity.Instance.IO.DataArea
    lastErrorMsg = CustomerContactDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction

;;; <summary>
;;; get_customercontact_table
;;; return the Customercontact data as a Array List
;;; </summary>
;;; <remarks>
;;; Return the contents of the customer_contact file as an array list which is
;;; then converted to a DataTable by xfNetLink.NET
;;; </remark>
;;; <param name=customerID>custoner ID to filter on</param>
;;; <param name=customerContactTable>returned collection of matching contacts</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_customercontact_table  ,boolean
    required in  userToken              ,a22
    required in  customerID             ,a10
    required out customerContactTable   ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;create the collection
    if (customerID) then
        customerContactTable = CustomerContactDataEntity.Instance.DataCollection.Collection(customerID, Q_PRIMARY)
    else
        customerContactTable = CustomerContactDataEntity.Instance.DataCollection.Collection()

    freturn true

endfunction

;;; <summary>
;;; get_location_contacts
;;; </summary>
;;; <remarks>
;;; Return a table of all of the contacts for a customer location.
;;; </remark>
;;; <param name=userToken>Token for logged on user</param>
;;; <param name=customerID>Customer ID to filter on.</param>
;;; <param name=locationID>Customer location ID to filter on.</param>
;;; <param name=customerContactTable>Returned collection of matching contacts</param>
;;; <return type="Boolean">true = success, false = error</returns>
function get_location_contacts  ,boolean
    required in  userToken              ,a22
    required in  customerID             ,a10
    required in  locationID             ,d2
    required out customerContactTable   ,@ArrayList
    endparams

    .include "DEF:Server.def"

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    customerContactTable = CustomerContactDataEntity.Instance.DataCollection.Collection(customerID+string(locationID,"XX"),Q_ALT1)

    freturn true

endfunction

;;; <summary>
;;; update_customercontact_record
;;; </summary>
;;; <remarks>
;;; pass in the modified record and attempt an update
;;; </remark>
;;; <param name=customerContactData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function update_customer_contact        ,boolean
    required in  userToken              ,a22
    required in  customerContactData    ,sContact
    required out newGrfa                ,a22
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerContactDataEntity.Instance.IO.DataArea = customerContactData

    ;;update the record
    result = CustomerContactDataEntity.Instance.IO.CompareAndSave()
    lastErrorMsg = CustomerContactDataEntity.Instance.IO.LastErrorMessage

    newGrfa = CustomerContactDataEntity.Instance.IO.GRFA

    freturn result

endfunction

;;; <summary>
;;; delete_customer_contact
;;; </summary>
;;; <remarks>
;;; pass in the record to delete
;;; </remark>
;;; <param name=customerContactData>passed record area</param>
;;; <return type="Boolean">true = success, false = error</returns>
function delete_customer_contact        ,boolean
    required in  userToken              ,a22
    required in  customerContactData    ,sContact
    endparams

    .include "DEF:Server.def"

    stack record
        result  ,boolean
    endrecord

proc
    ;;check we have a valid logged in user
    if (!%CheckLogin(userToken))
        freturn false

    ;;set the data area
    CustomerContactDataEntity.Instance.IO.DataArea = customerContactData

    ;;delete the record
    result = CustomerContactDataEntity.Instance.IO.FindAndDelete()
    lastErrorMsg = CustomerContactDataEntity.Instance.IO.LastErrorMessage

    freturn result

endfunction
