;;**********************************************************************
;;
;; Title:       CustomerDataEntity.dbc
;;
;; Type:        Class
;;
;; Description: Class to allow access to the customer data file.
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    public sealed class CustomerDataEntity extends SynPSG.Core.DataEntities.SDMSBase

        ;;allow the class to be referenced as a singleton
        private static mInternalData    ,@CustomerDataEntity

        public static property Instance ,@CustomerDataEntity
        method get
        proc
            if (mInternalData == ^null)
                mInternalData = new CustomerDataEntity(true, TableView.Full)
            mreturn mInternalData
        endmethod
        endproperty

        ;;-------------------------------------------------------------------------
        ;;private fields
        ;;-------------------------------------------------------------------------

        ;;;private user record area
        .include 'customer' repository, private record = "mCustomer", prefix="m"  ,end
        .include 'customer' repository, private structure = "sCustomer", prefix="s"  ,end

        ;;*************************************************************************

        ;;; <summary>
        ;;; Constructor
        ;;; </summary>
        ;;; <remarks>
        ;;; Constructor defines the file name and opens the file.  Creates the
        ;;; required event handler classes
        ;;; </remarks>
        ;;; <param name=viewType>type of table/collection view to provide</param>
        ;;; <param name=noDotNet>Pass TRUE to prevent the .NET framework being loaded</param>
        public method CustomerDataEntity
            in  req noDotNet            ,boolean
            in  req viewType            ,SynPSG.Core.DataEntities.TableView
            endparams
            parent("DAT:customer.ism",     ;;file name
            &   new CustomerPreStore(),     ;;pre store event handler
            &   ^null,                      ;;post store event handler
            &   new CustomerPreUpdate(),    ;;pre update event handler
            &   ^null,                      ;;update conflict event handler
            &   new CustomerFormatTrans(),  ;;format transaction view event handler
            &   new CustomerOnDelete(),     ;;on delete event handler
            &   viewType,                   ;;table/collection view type
            &   "Customer",                 ;;tableName
            &   new CustomerDefineTable(),  ;;define DataTable columns event handler
            &   new CustomerUpdateFields(), ;;field update event handler
            &   "CustomerCollection",       ;;collection name
            &   (@sCustomer) str,           ;;collection object
            &   ^null,                      ;;load collection event handler
            &   noDotNet)                   ;;do we load the framework?

            record
                str ,sCustomer
            endrecord
        proc

            this.IO.SetReplicationDetails("CUSTOMER", mCustomer, mCustomer.mreplication_key)

            mreturn

        endmethod

        ;;*************************************************************************

        ;;; <summary>
        ;;; Expose the data as a DataTable object
        ;;; </summary>
        ;;; <remarks>
        ;;; Expose the data as a DataTable object.  This method returns
        ;;; all data in the file as a DataTable unless the filter argument is passed.
        ;;; </remarks>
        ;;; <param name=primaryKey>optionally pass a filter on the primary key</param>
        ;;; <returns>Populated DataTable</returns>
.ifdef OS_WINDOWSNT
        public method ResultsTable  ,@System.Data.DataTable
            in  req viewType        ,SynPSG.Core.DataEntities.TableView
            in  opt primaryKey      ,a
            endparams

            record locrec
                row         ,@DataRow
                status      ,boolean
                gotKey      ,boolean
                tmpID       ,a^size(mCustomer.mCustomer_id)
                tmpViewType ,SynPSG.Core.DataEntities.TableView
            endrecord
        proc

            init locrec
            this.DataTable.Clear()

            ;;now add the actual data
            if (^passed(primaryKey))
            then
            begin
                gotKey = true
                tmpID = primaryKey
                status = this.IO.Locate(tmpID, Q_PRIMARY, false)
            end
            else
            begin
                gotKey = false
                status = this.IO.ReadFirst(Q_PRIMARY, false)
            end

            tmpViewType = viewType

            while ((status && !gotKey) || (status && gotKey && this.IO.PrimaryKeyValue .eq. tmpID))
            begin
                debugInfo("CustomerDataEntity::DataTable : Creating a new row")

                row = this.DataTable.Table.NewRow()

                mCustomer = this.IO.DataArea

                row[0] = Conversion.ToNetGRFA(this.IO.GRFA)
                row[1] = Conversion.ToNetString(mCustomer.mCustomer_ID)
                row[2] = Conversion.ToNetString(mCustomer.mCompany)

                using tmpViewType select
                (TableView.full),
                begin
                    row[3] = Conversion.ToNetInteger(mCustomer.mTech_Contact_ID)
                    row[4] = Conversion.ToNetInteger(mCustomer.mLegal_Contact_ID)
                    row[5] = Conversion.ToNetString(mCustomer.mCurrent_Contract)
                    row[6] = Conversion.ToNetString(mCustomer.mReplication_Key)

                end
                endusing

                this.DataTable.Table.Rows.Add(row)

                debugInfo("CustomerDataEntity::DataTable : Row added to table, key : " + mCustomer.mCustomer_ID)

                status = this.IO.ReadNext(false)
            end

            ;;commit the added rows to the table
            this.DataTable.Table.AcceptChanges()

            mreturn this.DataTable.Table

        endmethod
.endc
    endclass

endnamespace


