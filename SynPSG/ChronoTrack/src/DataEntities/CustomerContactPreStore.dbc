;;**********************************************************************
;;
;; Title:       CustomerContactPreStore.dbc
;;
;; Type:        Class
;;
;; Description: Class defining the PreStore for the customer
;;              contanct data entity event handler
;;
;; Author:      Richard C. Morris, Synergex Professional Services Group
;;
;; Copyright    © 2009 Synergex International Corporation.  All rights reserved.
;;
;; WARNING:     All content constituting or related to this code ("Code") is the
;;              property of Synergex International Corporation ("Synergex") and
;;              is protected by U.S. and international copyright laws.
;;              If you were given this Code by a Synergex employee then you may
;;              use and modify it freely for use within your applications.
;;
;;              However, you may use the Code only for your personal use.
;;              Any other use, unless otherwise authorized in writing by
;;              Synergex is strictly prohibited.  You may not under any
;;              circumstances distribute this Code, or any modified version
;;              or part of this Code, to any third party without first
;;              obtaining written permission to do so from Synergex.
;;              In using this Code you accept that it is provided as is,
;;              and without support or warranty of any kind.
;;
;;              Neither Synergex nor the author accept any responsibility
;;              for any losses or damages of any nature which may arise
;;              from the use of this Code.  This header information must
;;              remain unaltered in the Code at all times.  Possession
;;              of this Code, or any modified version or part of this Code,
;;              indicates your acceptance of these terms.
;;
;;***********************************************************************************
;;
;; ChronoTrack is an application written and managed by the Synergex Professional Services Group.
;;
;;***********************************************************************************

.include "DEF:ChronoTrack.def"

namespace SynPSG.ChronoTrack.DataEntities

    ;;;<summary>
    ;;;Event handling class.  Used to allow DataEntity classes to define
    ;;;an event handling class which will be executed prior to a store operation.
    ;;;This allows for additional processing proir to the record being stored in the file
    ;;;</summary>
    public class CustomerContactPreStore extends SynPSG.Core.DataEntities.PreStore

        ;;;<summary>
        ;;;Method that must be implemented by the DataEntity class when PreStoreProcessing
        ;;;events are required to be handled.
        ;;;</summary>
        ;;;<param name=sender>DataEntiry object</param>
        ;;;<param name=e>PreStoreEventArgs</param>
        public override method EventMethod   ,void
            in      req sender              ,@SynPSG.Core.DataEntities.SDMSio
            inout   req e                   ,@SynPSG.Core.DataEntities.PreStoreEventArgs
            endparams

            .include "customer_contact" repository, record = "contact" ,end

            record localRecord
                status  ,boolean
                index   ,int
            endrecord

        proc
            contact = e.NewData
            init localRecord

            debugInfo("CustomerContactPreStore::EventMethod() : Attempt to locate last record using key " + contact.customer_ID)

            ;;read through all customer contacts for the matching customer
            status = e.IOObject.Locate(contact.customer_ID, Q_PRIMARY, false)
            while (status && e.IOObject.PrimaryKeyValue .eq. contact.customer_ID)
            begin
                ;;get the data read
                contact = e.IOObject.DataArea
                ;;store the index
                index = contact.contact_ID

                debugInfo("CustomerContactPreStore::EventMethod() : Found : " + contact.customer_ID + " index " + string(contact.contact_ID))

                ;;see if there are any further matching records
                status = e.IOObject.ReadNext(false)
            end

            ;;increment the index counter
            index += 1

            ;restore the pssed data area
            contact = e.NewData
            contact.contact_ID += index

            ;;data integrity validations
            upcase contact.customer_id
            upcase contact.last_name
            locase contact.email
            if (!contact.friendly_name)
                contact.friendly_name = %atrim(contact.first_name) + " " + contact.last_name

            ;set unique key for SQL replication
            contact.replication_key = %datetime()

            debugInfo("CustomerContactPreStore::EventMethod() : setting index to " + string(contact.contact_ID))

            e.NewData = contact

            mreturn

        endmethod
    endclass

endnamespace
